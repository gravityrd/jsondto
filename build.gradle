plugins {
    id 'java'
    id 'signing'
    id 'maven'
}

group = "com.gravityrd"
description = """webshop data"""
version = '1.2.11'

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.7
targetCompatibility = 1.7
tasks.withType(JavaCompile) { options.incremental = true }


repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.0'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives javadocJar, sourcesJar, jar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {

                name 'jsondto'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'JSON data access objects for gravity java client.'
                url 'https://gravityrd.com'

                scm {
                    connection 'https://github.com/gravityrd/jsondto'
                    developerConnection 'git@github.com:gravityrd/jsondto.git'
                    url 'https://github.com/gravityrd/jsondto.git'
                }

                licenses {
                    license {
                        name 'GNU General Public License, version 2'
                        url 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'gravityrd'
                        name 'Gravity R&D'
                        email 'bence.sipos@sgravityrd.com'
                    }
                    developer {
                        id 'bernat'
                        name 'Gábor Bernát'
                        email 'gabor.bernat@gravityrd.com'
                    }
                }
            }
        }
    }
}
